/datum/objective/personal/literacy
	name = "Aprender a leer"
	category = "Elegido de Noc"
	triumph_count = 2
	rewards = list("2 Triunfos", "Noc se fortalece", "Conocimientos de matematicas")

/datum/objective/personal/literacy/on_creation()
	. = ..()
	if(owner?.current)
		RegisterSignal(owner.current, COMSIG_SKILL_RANK_CHANGE, PROC_REF(on_skill_increased))
	update_explanation_text()

/datum/objective/personal/literacy/Destroy()
	UnregisterSignal(owner.current, COMSIG_SKILL_RANK_CHANGE)
	return ..()

/datum/objective/personal/literacy/proc/on_skill_increased(datum/source, datum/attribute/skill/skill_ref, new_level, old_level)
	SIGNAL_HANDLER
	if(completed)
		return

	if(ispath(skill_ref, /datum/attribute/skill/misc/reading) && old_level == SKILL_RANK_NONE && new_level > SKILL_RANK_NONE)
		complete_objective()

/datum/objective/personal/literacy/complete_objective()
	. = ..()
	to_chat(owner.current, span_greentext("¡Has aprendido a leer, completando el objetivo de Noc!"))
	adjust_storyteller_influence(NOC, 20)
	UnregisterSignal(owner.current, COMSIG_SKILL_RANK_CHANGE)

/datum/objective/personal/literacy/reward_owner()
	. = ..()
	owner.current.adjust_skill_level(/datum/attribute/skill/labor/mathematics, 10)

/datum/objective/personal/literacy/update_explanation_text()
	explanation_text = "¡Deja atras tu ignorancia! ¡Aprende a leer para complacer a Noc!"
