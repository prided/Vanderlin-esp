/datum/objective/personal/ravox_duel
	name = "Duelos de honor"
	category = "Elegido de Ravox"
	triumph_count = 3
	immediate_effects = list("Obtuviste una habilidad para desafiar a otros")
	rewards = list("3 Triunfos", "Ravox se fortalece", "Ravox te bendice (+1 Fuerza)")
	var/duels_won = 0
	var/duels_required = 1

/datum/objective/personal/ravox_duel/on_creation()
	. = ..()
	if(owner?.current)
		var/datum/action/cooldown/spell/ravox_challenge/challenge_spell = new(src)
		challenge_spell.Grant(owner.current)
	update_explanation_text()

/datum/objective/personal/ravox_duel/proc/on_duel_won()
	duels_won++
	if(duels_won >= duels_required && !completed)
		complete_objective()

/datum/objective/personal/ravox_duel/complete_objective()
	. = ..()
	to_chat(owner.current, span_greentext("¡Has demostrado tu valia en combate! ¡Ravox esta complacido!"))
	adjust_storyteller_influence(RAVOX, 20)

/datum/objective/personal/ravox_duel/reward_owner()
	. = ..()
	owner.current.adjust_stat_modifier(STATMOD_RAVOX_BLESSING, list(STAT_STRENGTH = 1))

/datum/objective/personal/ravox_duel/update_explanation_text()
	explanation_text = "¡Gana con honor [duels_required] duelo\s contra otros guerreros para demostrar tu poder!"

/datum/duel
	var/ongoing = TRUE
	var/datum/weakref/challenger
	var/datum/weakref/challenged
	var/datum/weakref/objective

/datum/duel/New(mob/living/carbon/human/challenger, mob/living/carbon/human/challenged, datum/objective/personal/ravox_duel/listener)
	src.challenger = WEAKREF(challenger)
	src.challenged = WEAKREF(challenged)
	objective = WEAKREF(listener)

	addtimer(CALLBACK(src, PROC_REF(end_duel)), 8 MINUTES, TIMER_DELETE_ME)
	addtimer(CALLBACK(src, PROC_REF(check_duel)), 4 SECONDS, TIMER_UNIQUE|TIMER_OVERRIDE)

/datum/duel/Destroy(force)
	challenger = null
	challenged = null
	return ..()

/datum/duel/proc/check_duel()
	if(QDELETED(src) || !ongoing)
		return
	var/mob/living/carbon/human/challenger_mob = challenger.resolve()
	var/mob/living/carbon/human/challenged_mob = challenged.resolve()
	if(QDELETED(challenger_mob) || QDELETED(challenged_mob))
		qdel(src)

	if(challenger_mob.surrendering || challenger_mob.incapacitated(IGNORE_GRAB))
		challenged_mob.visible_message(span_notice("¡[challenged_mob] derrota a [challenger_mob] en el duelo de honor!"))
		finish_duel(challenger_mob, challenged_mob)
		return
	else if(challenged_mob.surrendering || challenged_mob.incapacitated(IGNORE_GRAB))
		challenger_mob.visible_message(span_notice("¡[challenger_mob] derrota a [challenged_mob] en el duelo de honor!"))
		finish_duel(challenged_mob, challenger_mob)
		return

	addtimer(CALLBACK(src, PROC_REF(check_duel)), 4 SECONDS, TIMER_UNIQUE|TIMER_OVERRIDE)

/datum/duel/proc/end_duel()
	if(QDELETED(src) || !ongoing)
		return
	ongoing = FALSE
	var/mob/living/carbon/human/challenger_mob = challenger.resolve()
	if(challenger_mob)
		to_chat(challenger_mob, span_notice("¡El duelo ha durado demasiado y se declara un empate!"))
	var/mob/living/carbon/human/challenged_mob = challenged.resolve()
	if(challenged_mob)
		to_chat(challenged_mob, span_notice("¡El duelo ha durado demasiado y se declara un empate!"))

	qdel(src)

/datum/duel/proc/finish_duel(mob/living/loser, mob/living/winner)
	if(QDELETED(src) || !ongoing)
		return
	ongoing = FALSE

	to_chat(loser, span_red("¡Has perdido el duelo de honor!"))
	to_chat(winner, span_green("¡Has ganado el duelo de honor!"))

	if(objective)
		var/datum/objective/personal/ravox_duel/ravox = objective.resolve()
		if(ravox?.owner == winner.mind)
			ravox.on_duel_won()

	qdel(src)
